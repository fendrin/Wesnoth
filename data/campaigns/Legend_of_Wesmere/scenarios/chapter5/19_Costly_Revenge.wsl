--textdomain wesnoth-low
Scenario{
    name: _ "Costly Revenge"

    id: "19_Costly_Revenge"
    victory_when_enemies_defeated: false
    experience_modifier: 100

    random_start_time: false
    allow_new_game: true
    new_game_title: _ "Chapter Five: Civil War"
    force_lock_settings: true

    <LOW_MAP("Saurgrath.map")
    <WINTER_MASK("19_Costly_Revenge.mask")

    <TURNS(40, 35, 30)

    <INTRO_AND_SCENARIO_MUSIC("revelation.ogg", "the_deep_path.ogg")
    <EXTRA_SCENARIO_MUSIC("vengeful.ogg")
    <EXTRA_SCENARIO_MUSIC("casualties_of_war.ogg")
    <EXTRA_SCENARIO_MUSIC("the_dangerous_symphony.ogg")
    <EXTRA_SCENARIO_MUSIC("into_the_shadows.ogg")
    <EXTRA_SCENARIO_MUSIC("weight_of_revenge.ogg")
    <EXTRA_SCENARIO_MUSIC("nunc_dimittis.ogg")
    <EXTRA_SCENARIO_MUSIC("suspense.ogg")

    <DEFAULT_SCHEDULE_DUSK!

    next_scenario: "20_Council_Ruling"
    story: {
        <if MULTIPLAYER then {
            part: {
                delay: 4000
                title: _ "<i>The Legend of Wesmere</i>
Chapter Five"
            }
        }
        part: {
            title: _ "Chapter Five
<i>Civil War</i>"
        }
        part: {
            show_title: true
            story: _ "But Kalenz failed to persuade the dwarves. The dwarves left, and Landar insisted the elves must march on the empire of the Saurians..."
            delay: 4000
            <LOW_BIGMAP!
            <SAURIANS_STAGE1!
        }
        part: {
            story: CAPTION(_ "Cleodil") .. _ "Kalenz... the shydes and druids are not happy with this. The saurians are already beaten, and there is too much hate in Landar’s heart. Something is not right here."
            background: "story/characters/cleodil.png"
            scale_background: false
        }
        part: {
            story: CAPTION(_ "Kalenz") .. _ "I am not entirely easy with this myself, Cleodil. But there is something to what Landar says; the wide green world must know there is a price, a heavy price for raiding in the forest of Wesmere."
            background: "story/characters/kalenz.png"
            scale_background: false
        }
        part: {
            story: CAPTION(_ "Cleodil") .. _ "Then you who walk the earth path and carry steel can collect that price. Those on the faerie path will not be with you. Not this time."
            background: "story/characters/cleodil.png"
            scale_background: false
        }
        part: {
            story: CAPTION(_ "Kalenz") .. _ "We do as we must."
            background: "story/characters/kalenz.png"
            scale_background: false
        }
    }
    <LOW_TRACK(SAURIANS_STAGE1_END!)

    PLAYER_GOLD: () -> {
        <GOLD(300, 260, 220)
    }
    MULTIPLAYER_GOLD: () -> {
        <GOLD(150, 130, 110)
    }

    side: {
        side: 1
        <PLAYABLE!
        save_id: "Kalenz"

        <KALENZ!
        x: 3
        y: 18

        <unless MULTIPLAYER! then {
            <PLAYER_GOLD!
            unit: {
                <LANDAR!
--TODO place on his keep
                placement: "leader"
            }
        } else {
            <MULTIPLAYER_GOLD!
        }
    }
    event: {
        name: "prestart"

        <unless MULTIPLAYER! then {
            kill: {
                race: "dwarf"
            }
        }

--TODO see if this recalls units wich are not recallable
        do: ->
            RECALL_LOYALS!
    }

    side: {
        side: 2
        controller: "ai"
        allow_player: false
        type: "Saurian Flanker"
        id: "Spahr"
        name: _ "Spahr"
        canrecruit: true
        x: 23
        y: 20
        <GOLD(300, 500, 600)
        <INCOME(4, 8, 12)
        team_name: "Saurians"
        user_team_name: _ "Enemies"
        <SAURIAN_SETUP!
        <if EASY then {
            recruit: {"Saurian Skirmisher", "Saurian Augur"}
        } else {
            recruit: {"Saurian Skirmisher", "Saurian Augur", "Saurian Ambusher", "Saurian Oracle", "Saurian Soothsayer", "Saurian Flanker"}
        }

        ai: {
            <if EASY then {
                recruitment_pattern: {"scout", "scout", "healer", "scout"}
            } else {
                recruitment_pattern: {"scout", "scout", "healer", "archer", "archer", "scout"}
            }
        }
    }
    <STARTING_VILLAGES_ALL(2)

    side: {
        side: 3
        controller: "ai"
        allow_player: false
        type: "Saurian Flanker"
        id: "Hgyr"
        name: _ "Hgyr"
        canrecruit: true
        x: 26
        y: 2
        <GOLD(250, 300, 400)
        <INCOME(4, 8, 12)
        team_name: "Saurians"
        user_team_name: _ "Enemies"
        <SAURIAN_SETUP!
        <if EASY then {
            recruit: {"Saurian Skirmisher", "Saurian Augur"}
        } else {
            recruit: {"Saurian Skirmisher", "Saurian Augur", "Saurian Ambusher", "Saurian Flanker", "Saurian Oracle", "Saurian Soothsayer"}
        }

        ai: {
            <if EASY then {
                recruitment_pattern: {"scout", "healer", "scout"}
            } else {
                recruitment_pattern: {"scout", "healer", "scout", "archer"}
            }
        }
    }

    <if MULTIPLAYER then {
        side: {
            side: 4
            <PLAYABLE!

            <MULTIPLAYER_GOLD!
            save_id: "Landar"
            no_leader: true
            unit: {
                <LANDAR!
                x: 3
                y: 18
            }
        }
    }

    <STARTING_VILLAGES(3, 20)

    event: {
        name: "victory"

-- dialog
        do: ->
            Message{
                id: "Kalenz"
                message: _ "That was the last. A bitter day’s work, and no memory to be proud of."
            }
            Message{
                id: "Landar"
                message: WHISPER(_ "A shame we missed the opportunity to rid the forests of dwarves, as well.")
            }
    }

------ Scenario objectives -----
    event: {
        name: "prestart"
        do: ->
            Objectives{
                side: 0
                objective: {
                    description: _ "Defeat all enemy units, and destroy all villages"
                    condition: "win"
                }
                objective: {
                    description: _ "Death of Kalenz"
                    condition: "lose"
                }
                objective: {
                    description: _ "Death of Landar"
                    condition: "lose"
                }

                <TURNS_RUN_OUT!

                gold_carryover: {
                    carryover_percentage: 0
                }
            }
    }

    event: {
        name: "start"
        do: ->
            Message{
                side: 1
                not: {
                    id: "Kalenz"
                }
                not: {
                    id: "Landar"
                }
                <FILTER_LOYALS!
                message: _ "We have finally reached Saurgrath, their capital. Remember whose side they have taken in the war against us."
            }
            Message{
                id: "Landar"
                message: _ "Now is our chance to finish them off..."
            }
            Message{
                id: "Kalenz"
                message: _ "I fear this will be a bloodbath..."
            }
            Message{
                id: "Landar"
                message: _ "Destroy them, root and branch. Burn out their homes. Let none remain alive!"
            }

            Message{
                speaker: "narrator"
                image: "wesnoth-icon.png"
                message: _ "Kalenz is not able to recruit or recall shamans or any of their advancements in this scenario."
            }
    }

--TODO update this comment
-- every time one of the saurian gets killed this event checks if it
-- was the last one and if there are any villages left.
    VICTORY_CONDITIONAL: () -> {
        if: {
            not: {
                have_unit: {
                    side: 2
                    or: {
                        side: 3
                    }
                }
            }
            not: {
                have_location: {
                    terrain: "*^V*"
                }
            }
            then: ->
                Endlevel{
                    result: "victory"
                    bonus: false
                    save: false
                    carryover_percentage: 0
                }
            
        }
    }

    event: {
        name: "die"
        first_time_only: false
        filter: {
            race: "lizard"
        }
        do: ->
            VICTORY_CONDITIONAL!
    }

    event: {
        name: "capture"
        first_time_only: true
        filter: {
            side: 1
            not: {
                id: "Landar"
            }
        }
--TODO check ingame if the dialogue works.
        do: ->
            Message{
                x: x1
                y: y1
                message: _ "These are but eggs and hatchlings."
            }
            Message{
                id: "Landar"
                message: _ "Do not spare them. They will only foul our forests anew when they get their growth."
            }
            Message{
--TODO The nearest saurian
--formula=TODO get it from crab
                side: {2, 3}
                not: {
                    canrecruit: true
                }
                message: _ "They even shatter our eggs!"
            }
            If{
                have_unit: {
--wmllint: recognize Anduilas
                    id: "Anduilas"
                }
                then: ->
                    Role{
                        role: "Anduilas"
                        id: "Anduilas"
                    }
                
                else: ->
                    Role{
                        role: "Anduilas"
                        side: 1
                        not: {
                            id: "Kalenz"
                        }
                        not: {
                            id: "Landar"
                        }
                        not: {
                            id: unit.id
                        }
                    }
                
            }
            Message{
                role: "Anduilas"
                message: _ "But... to kill their young? Are we to go that far?"
            }
            Message{
                id: "Kalenz"
                message: _ "Stay. You need not have the blood of children on your hands, and I shall not."
            }
            Message{
                id: "Landar"
                message: _ "Perhaps you are too tender-minded to do what must be done, but many of us are not."
            }
            Message{
                id: "Kalenz"
                message: _ "I will not set elf against elf. But, Landar, I fear you are storing up a dreadful price for yourself."
            }
            Message{
                id: "Landar"
                message: _ "Stand aside. I will do what is needful."
            }
            Message{
                side: {2, 3}
                not: {
                    canrecruit: true
                }
                message: _ "Their revenge is terrible! I fear for our kind."
            }
            Message{
                side: {2, 3}
                canrecruit: true
                message: _ "Don’t flee. We cannot abandon our capital."
            }
            Scroll_To{
                x: x1
                y: y1
            }
    }

-- capture event lifted from IftU.
    event: {
        name: "moveto"
        first_time_only: false
        filter: {
            filter_location: {
                terrain: "*^V*"
            }
            side: 1
            not: {
                id: "Kalenz"
            }
            not: {
                role: "Anduilas"
            }
        }
        do: ->
            Sound{
                name: "hiss-die.wav"
            }
--TODO need a delay here?
            Sound{
                name: "torch.ogg"
            }
            Delay{
                time: 200
            }
            Sound{
                name: "wose-die.ogg"
            }
            Terrain{
                x: x1
                y: y1
                terrain: "Dd^Dr"
                layer: "overlay"
            }
            Redraw{}
            VICTORY_CONDITIONAL!
    }

    INCLUDE("campaigns/Legend_of_Wesmere/utils/deaths.cfg")
}

PLAYER_GOLD = nil
MULTIPLAYER_GOLD = nil
