--textdomain wesnoth-thot

Scenario{
    id: "10_The_Siege_of_Kal_Kartha"
    name: _ "The Siege of Kal Kartha"
    map: "campaigns/The_Hammer_of_Thursagan/maps/10_The_Siege_of_Kal_Kartha.map"
    turns: 35
    next_scenario: "11_The_Court_of_Karrag"
    victory_when_enemies_defeated: true

    <DEFAULT_SCHEDULE!

-- The map should be thematically something like a reflection of the one
-- for "At The East Gate", but on a larger scale.  The player’s
-- encampment should be at the west edge.  Kal Kartha will be
-- represented by an underground keep (the West Gate) set into
-- high mountains at the east edge of the map.

    <SCENARIO_MUSIC("the_dangerous_symphony.ogg")
    <EXTRA_SCENARIO_MUSIC("loyalists.ogg")
    <EXTRA_SCENARIO_MUSIC("northerners.ogg")

    <THOT_TRACK(JOURNEY_STAGE10!)

-- wmllint: validate-off
    side: {
        side: 1
        controller: "human"
        recruit: "Dwarvish Fighter,Dwarvish Thunderer,Dwarvish Guardsman, Dwarvish Scout"
        <GOLD(140, 120, 100)
        team_name: "good"
        user_team_name: _ "Alliance"
        <FLAG_VARIANT("knalgan")

-- wmllint: recognize Aiglondur
        <CHARACTER_STATS_AIGLONDUR!

        facing: "ne"
    }

    side: {
        side: 2
        controller: "ai"
        recruit: "Dwarvish Fighter,Dwarvish Thunderer"
        <GOLD(320, 320, 320)
        team_name: "good"
        user_team_name: _ "Alliance"
        <FLAG_VARIANT("knalgan")

-- wmllint: recognize Dulcatulos
        <CHARACTER_STATS_DULCATULOS_AS_LEADER!

        facing: "sw"

        ai: {
            <NO_SCOUTS!
            passive_leader: true
            grouping: "defensive"
            caution: 2.0
            recruitment_pattern: {"fighter", "fighter", "mixed fighter"}
            <ATTACK_DEPTH(4, 5, 5)
        }
    }
-- wmllint: validate-on

    side: {
        side: 3
        controller: "ai"
        <if EASY then {
            recruit: {"Orcish Archer", "Orcish Grunt", "Wolf Rider", "Orcish Crossbowman", "Goblin Pillager", "Goblin Spearman"}
        }

        <if NORMAL then {
            recruit: {"Orcish Archer", "Orcish Grunt", "Wolf Rider", "Orcish Crossbowman", "Orcish Warrior", "Goblin Pillager", "Goblin Spearman"}
        }

        <if HARD then {
            recruit: {"Orcish Archer", "Orcish Grunt", "Wolf Rider", "Orcish Crossbowman", "Orcish Warrior", "Goblin Knight", "Goblin Pillager", "Orcish Slayer", "Goblin Spearman", "Goblin Impaler"}
        }
        <GOLD(160, 200, 240)
        team_name: "orcs"
        user_team_name: _ "Orcs"
        <FLAG_VARIANT6("ragged")

        type: "Orcish Warlord"
        id: "Tan-Morgh"
        name: _ "Tan-Morgh"
        profile: "portraits/orcs/grunt-2.png"
        canrecruit: true

        facing: "sw"

        ai: {
            <NO_SCOUTS!
            <unless HARD! then {
                recruitment_pattern: {"scout", "fighter", "fighter", "archer"}
            } else {
                recruitment_pattern: {"scout", "fighter", "fighter", "mixed fighter", "archer"}
            }
            <ATTACK_DEPTH(4, 5, 5)

            leader_goal: {
                x: 44, y: 15   -- wants to take the Kal Karthans' keep.
            }

            goal: {
                name: "target"
                criteria: {
                    side: 2 -- wants to kill Kal Karthans
                }
                value: 4.0
            }
        }
        ai: {
            time_of_day: {"dusk", "first_watch", "second_watch"}
            aggression: 0.75
            caution: 0.0
            grouping: false
        }
    }

    side: {
        side: 4
        controller: "ai"
        recruit: {"Orcish Archer", "Orcish Grunt", "Wolf Rider", "Orcish Crossbowman", "Goblin Pillager", "Goblin Spearman"}
        <GOLD(160, 200, 240)
        team_name: "orcs"
        user_team_name: _ "Orcs"
        <FLAG_VARIANT6("ragged")

        type: "Orcish Warlord"
        id: "Tan-Garukh"
        name: _ "Tan-Garukh"
        profile: "portraits/orcs/grunt-2.png"
        canrecruit: true

        facing: "ne"

        ai: {
            <NO_SCOUTS!
            recruitment_pattern: {"scout", "fighter", "fighter", "archer"}
            <ATTACK_DEPTH(4, 5, 5)

            leader_goal: {
                x: 44, y: 15  -- wants to take the Kal Karthans' keep.
            }

            goal: {
                name: "target"
                value: 4.0
                criteria: {
                    side: 2 -- wants to kill Kal Karthans
                }
            }
        }
        ai: {
            time_of_day: {"dusk", "first_watch", "second_watch"}
            aggression: 0.75
            caution: 0.0
            grouping: false
        }
    }

    side: {
        side: 5
        controller: "ai"
        recruit: {"Orcish Archer", "Orcish Grunt", "Wolf Rider", "Orcish Warrior", "Goblin Spearman"}
        <GOLD(160, 200, 240)
        team_name: "orcs"
        user_team_name: _ "Orcs"
        <FLAG_VARIANT6("ragged")

        type: "Orcish Warlord"
        id: "Tan-Wagran"
        name: _ "Tan-Wagran"
        profile: "portraits/orcs/warlord.png"
        canrecruit: true

        facing: "ne"

        ai: {
            <NO_SCOUTS!
            recruitment_pattern: {"scout", "fighter", "fighter", "archer"}
            <ATTACK_DEPTH(4, 5, 5)

            leader_goal: {
                x: 44, y: 15  -- wants to take the Kal Karthans' keep.
            }

            goal: {
                name: "target"
                value: 4.0
                criteria: {
                    side: 2 -- wants to kill Kal Karthans
                }
            }
        }
        ai: {
            time_of_day: {"dusk", "first_watch", "second_watch"}
            aggression: 0.75
            caution: 0.0
            grouping: false
        }
    }

    <STARTING_VILLAGES(2, 5)

    label: {
        x: 44, y: 15
        text: _ "West Gate"
    }

    event: {
        name: "prestart"
        do: ->
            Objectives{
                side: 1
                objective: {
                    description: _ "Defeat all enemies"
                    condition: "win"
                }
                objective: {
                    description: _ "Death of Aiglondur"
                    condition: "lose"
                }
                objective: {
                    description: _ "Death of Angarthing"
                    condition: "lose"
                }
                objective: {
                    description: _ "Death of Dulcatulos"
                    condition: "lose"
                }

                <TURNS_RUN_OUT!

                gold_carryover: {
                    bonus: true
                    carryover_percentage: 40
                }
            }

            Recall{
                id: "Angarthing"
            }

            Recall{
                id: "Ratheln"
            }

            Recall{
                id: "Darchas"
            }

            Recall{
                id: "Lorinne"
            }

            MODIFY_UNIT({side: 1}, "facing", "ne")
    }

    <CONDITIONAL_MAGE_RECRUITING!

    event: {
        name: "start"
        do: ->
            Message{
                speaker: "Angarthing"
                message: _ "Behold! The West Gate of Kal Kartha!"
            }

            Message{
                speaker: "Aiglondur"
                message: _ "And under siege, I see. There is a mighty host of orcs between us and that gate."
            }

            Message{
                speaker: "Tan-Morgh"
                message: _ "Ahhh... more stinky-midgets, come to get killed just like these cowards in their den."
            }

            Message{
                speaker: "Aiglondur"
                message: _ "Let us bring our kin that orc’s head as a guesting-gift."
            }
    }

-- Dulcatulos gets a castle of reinforcements about the
-- time the orcs will have killed off his first wave.
    event: {
        name: "turn 7"
        do: ->
            Gold{
                amount: 120
                side: 2
            }

            Message{
                speaker: "Dulcatulos"
                message: _ "Kal Kartha shall not fall!"
            }
    }

    event: {
        name: "die"
        filter: {
            id: "Dulcatulos"
        }
        do: ->
            Message{
                speaker: "Aiglondur"
                message: _ "Kal Kartha is taken. Our mission has failed."
            }

            Endlevel{
                result: "defeat"
            }
    }

    event: {
        name: "enemies defeated"
        do: ->
            Message{
                speaker: "Dulcatulos"
                message: _ "Well fought, and our thanks; we were sore pressed. You are our honored guests, and I will show you to the best quarters we have myself. Our lord, the runemaster Karrag, will want to have speech with you on the morrow."
            }

            Message{
                speaker: "Angarthing"
                message: _ "A runemaster? There had been none such since Thursagan’s day. And for one to lead a holding was unheard-of; they tended towards the solitary life."
            }

            Message{
                speaker: "Dulcatulos"
                message: _ "Aye? Well, you’d know such things better than I, loremaster. Our Karrag toiled for long years to recover the craft lore. He was elevated when our old lord fell in battle against the besieging orcs. Karrag himself was wounded near to death; none thought he would recover. But he leads us today, and his runelore has oft been the only shield between us and the orcs."
            }

            Message{
                speaker: "Angarthing"
                message: _ "He has grasped the Hammer of Thursagan, then?"
            }

            Message{
                speaker: "Dulcatulos"
                message: _ "Aye. Toils over it in a workshop in the underlevels with his personal followers. He has promised all of us that the Hammer’s power will smash and scatter all our enemies."
            }

            Message{
                speaker: "Angarthing"
                message: _ "That is strange. The Hammer is a tool of crafting and making, not a weapon. What can he mean to do with it?"
            }

            Message{
                speaker: "Dulcatulos"
                message: _ "That’s for lords and loremasters to worry about, not the likes of me. I must see to my troop’s care. Food will be brought to you; rest well, we will speak again."
            }

            Endlevel{
                result: "victory"
                bonus: true
                <NEW_GOLD_CARRYOVER(40)
            }
    }

    <HERODEATH_AIGLONDUR!
    <HERODEATH_ANGARTHING!
    <HERODEATH_RATHELN!
}
